#@TYPE: Machine
#@NAME: iot2000

#@DESCRIPTION: Machine configuration for iot2000 systems

MACHINEOVERRIDES =. "intel-quark:"

# meta-intel switched to an own kernel source, but we stay with linux-yocto
# to have the 4.4 option with a smooth migration path to 4.4-cip
PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto"

require conf/machine/include/meta-intel.inc
require conf/machine/include/intel-quark-common.inc
require conf/machine/include/intel-common-pkgarch.inc

# Yocto 2.2 comes with 4.8, but that kernel is already EOL upstream
PREFERRED_VERSION_linux-yocto = "4.4%"
PREFERRED_VERSION_linux-yocto-rt = "4.4%"

#Avoid pulling in GRUB
MACHINE_ESSENTIAL_EXTRA_RDEPENDS = ""

MACHINE_FEATURES = "efi usb"

MACHINE_EXTRA_RRECOMMENDS += "kernel-modules linux-firmware"

SERIAL_CONSOLE = "115200 ttyS1"
APPEND += "rootwait console=ttyS1,115200 console=tty0"

EFI_PROVIDER ?= "systemd-boot"

# Ensure that the Lock prefix is omitted for the kernel"
KERNEL_EXTRA_ARGS = "EXTRA_CFLAGS=-Wa,-momit-lock-prefix=yes"

# Machine specific Image packages:
IMAGE_INSTALL_append = " kernel-modules"

# Package compatiblity:
PACKAGE_EXTRA_ARCHS_append = " intel-quark quark i586 x86"

EXTRA_IMAGEDEPENDS_append = " acpi-upgrades"
INITRD_LIVE_prepend = "${DEPLOY_DIR_IMAGE}/acpi-upgrades-${MACHINE}.cpio "

# glibc needs to be compiled with -Wa,-momit-lock-prefix for the iot2000 BSP
# force a rebuild with the following TCMODE setting and also get binary locales
# compiled
TCMODE = "external-sourcery-rebuild-libc"
ENABLE_BINARY_LOCALE_GENERATION = "1"
GLIBC_INTERNAL_USE_BINARY_LOCALE = "compile"

# Unfortunately a single binary locale package gets generated, we will want that
# one used in our images
IMAGE_LINGUAS = "en-us.iso-8859-1"

# Generate compressed EXT4 images for swupdate
IMAGE_FSTYPES += " ext4.gz"

MACHINE_FEATURES += "swupdate"
IMAGE_FSTYPES += "${@bb.utils.contains('COMBINED_FEATURES', 'swupdate', 'ext4.swu', '', d)}"
